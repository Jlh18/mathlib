/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import algebraic_geometry.open_immersion
import ring_theory.nilpotent
import topology.sheaves.sheaf_condition.sites
import category_theory.limits.constructions.binary_products
import algebra.category.CommRing.constructions
import ring_theory.integral_domain

/-!
# Basic properties of schemes

We provide some basic properties of schemes

## Main definition
* `algebraic_geometry.is_integral`: A scheme is integral if it is nontrivial and all nontrivial
  components of the structure sheaf are integral domains.
* `algebraic_geometry.is_reduced`: A scheme is reduced if all the components of the structure sheaf
  is reduced.
-/

open topological_space opposite category_theory category_theory.limits Top

namespace algebraic_geometry

variable (X : Scheme)

-- TODO: add sober spaces, and show that schemes are sober
instance : t0_space X.carrier :=
begin
  rw t0_space_iff_distinguishable,
  intros x y h h',
  obtain ‚ü®U, R, ‚ü®e‚ü©‚ü© := X.local_affine x,
  have hy := (h' _ U.1.2).mp U.2,
  erw ‚Üê subtype_indistinguishable_iff (‚ü®x, U.2‚ü© : U.1.1) (‚ü®y, hy‚ü© : U.1.1) at h',
  let e' : U.1 ‚âÉ‚Çú prime_spectrum R :=
    homeo_of_iso ((LocallyRingedSpace.forget_to_SheafedSpace ‚ãô SheafedSpace.forget _).map_iso e),
  have := t0_space_of_injective_of_continuous e'.injective e'.continuous,
  rw t0_space_iff_distinguishable at this,
  exact this ‚ü®x, U.2‚ü© ‚ü®y, hy‚ü© (by simpa using h) h'
end

/-- A scheme `X` is integral if its carrier is nonempty,
and `ùí™‚Çì(U)` is an integral domain for each `U ‚â† ‚àÖ`. -/
class is_integral : Prop :=
(nonempty : nonempty X.carrier . tactic.apply_instance)
(component_integral : ‚àÄ (U : opens X.carrier) [_root_.nonempty U],
  is_domain (X.presheaf.obj (op U)) . tactic.apply_instance)

attribute [instance] is_integral.component_integral is_integral.nonempty

/-- A scheme `X` is reduced if all `ùí™‚Çì(U)` are reduced. -/
class is_reduced : Prop :=
(component_reduced : ‚àÄ U, _root_.is_reduced (X.presheaf.obj (op U)) . tactic.apply_instance)

attribute [instance] is_reduced.component_reduced

lemma is_reduced_of_stalk_is_reduced [‚àÄ x : X.carrier, _root_.is_reduced (X.presheaf.stalk x)] :
  is_reduced X :=
begin
  refine ‚ü®Œª U, ‚ü®Œª s hs, _‚ü©‚ü©,
  apply presheaf.section_ext X.sheaf U s 0,
  intro x,
  rw ring_hom.map_zero,
  change X.presheaf.germ x s = 0,
  exact (hs.map _).eq_zero
end

instance stalk_is_reduced_of_reduced [is_reduced X] (x : X.carrier) :
  _root_.is_reduced (X.presheaf.stalk x) :=
begin
  constructor,
  rintros g ‚ü®n, e‚ü©,
  obtain ‚ü®U, hxU, s, rfl‚ü© := X.presheaf.germ_exist x g,
  rw [‚Üê map_pow, ‚Üê map_zero (X.presheaf.germ ‚ü®x, hxU‚ü©)] at e,
  obtain ‚ü®V, hxV, iU, iV, e'‚ü© := X.presheaf.germ_eq x hxU hxU _ 0 e,
  rw [map_pow, map_zero] at e',
  replace e' := (is_nilpotent.mk _ _ e').eq_zero,
  erw ‚Üê concrete_category.congr_hom (X.presheaf.germ_res iU ‚ü®x, hxV‚ü©) s,
  rw [comp_apply, e', map_zero]
end

lemma is_reduced_of_open_immersion {X Y : Scheme} (f : X ‚ü∂ Y) [H : is_open_immersion f]
  [is_reduced Y] : is_reduced X :=
begin
  constructor,
  intro U,
  have : U = (opens.map f.1.base).obj (H.base_open.is_open_map.functor.obj U),
  { ext1, exact (set.preimage_image_eq _ H.base_open.inj).symm },
  rw this,
  exact is_reduced_of_injective (inv $ f.1.c.app (op $ H.base_open.is_open_map.functor.obj U))
    (as_iso $ f.1.c.app (op $ H.base_open.is_open_map.functor.obj U) : Y.presheaf.obj _ ‚âÖ _).symm
      .CommRing_iso_to_ring_equiv.injective
end

local attribute [elementwise] category_theory.is_iso.hom_inv_id

lemma basic_open_eq_of_affine {R : CommRing}
  (f : R) :
  RingedSpace.basic_open (Spec.to_SheafedSpace.obj (op R)) ((Spec_Œì_identity.app R).inv f) =
    prime_spectrum.basic_open f :=
begin
  ext,
  change ‚Üë(‚ü®x, trivial‚ü© : (‚ä§ : opens _)) ‚àà
    RingedSpace.basic_open (Spec.to_SheafedSpace.obj (op R)) _ ‚Üî _,
  rw RingedSpace.mem_basic_open,
  suffices : is_unit (structure_sheaf.to_stalk R x f) ‚Üî f ‚àâ prime_spectrum.as_ideal x,
  { exact this },
  erw [‚Üê is_unit_map_iff (structure_sheaf.stalk_to_fiber_ring_hom R x),
    structure_sheaf.stalk_to_fiber_ring_hom_to_stalk],
  exact (is_localization.at_prime.is_unit_to_map_iff
    (localization.at_prime (prime_spectrum.as_ideal x)) (prime_spectrum.as_ideal x) f : _)
end

lemma basic_open_eq_of_affine' {R : CommRing}
  (f : (Spec.to_SheafedSpace.obj (op R)).presheaf.obj (op ‚ä§)) :
  RingedSpace.basic_open (Spec.to_SheafedSpace.obj (op R)) f =
    prime_spectrum.basic_open ((Spec_Œì_identity.app R).hom f) :=
begin
  convert basic_open_eq_of_affine ((Spec_Œì_identity.app R).hom f),
  exact (coe_hom_inv_id _ _).symm
end

lemma eq_zero_of_basic_open_empty {X : Scheme} [is_reduced X] {U : opens X.carrier}
  (s : X.presheaf.obj (op U)) (hs : X.to_LocallyRingedSpace.to_RingedSpace.basic_open s = ‚àÖ) :
  s = 0 :=
begin
  apply Top.presheaf.section_ext X.sheaf U,
  intro x,
  obtain ‚ü®_,‚ü®j,rfl‚ü©,hx,i‚ü© := X.affine_basis_cover_is_basis.exists_subset_of_mem_open x.prop U.2,
  let V : opens X.carrier := ‚ü®_, (X.affine_basis_cover.is_open j).base_open.open_range‚ü©,
  replace i : V ‚ü∂ U := hom_of_le i,
  let s' : (X.affine_basis_cover.obj j).presheaf.obj (op $ (opens.map _).obj _) :=
    ((X.affine_basis_cover.map j).1.c.app (op V) (X.presheaf.map i.op s) : _),
  have Veq : V = (X.affine_basis_cover.is_open j).base_open
    .is_open_map.functor.obj ‚ü®_, is_open_univ‚ü© := by simp [is_open_map.functor],
  have Veq' : (opens.map (X.affine_basis_cover.map j).val.base).obj V = ‚ä§,
  { rw Veq, ext1, exact set.preimage_image_eq _ (X.affine_basis_cover.is_open j).base_open.inj },
  suffices : s' = 0,
  { haveI : is_iso ((X.affine_basis_cover.map j).1.c.app (op V)),
    { rw Veq,
      exact PresheafedSpace.is_open_immersion.c_iso ‚ü®_, is_open_univ‚ü© },
    have : X.presheaf.map i.op s = 0,
    { apply_fun (inv $ (X.affine_basis_cover.map j).1.c.app _) at this,
      rwa [category_theory.is_iso.hom_inv_id_apply, map_zero] at this },
    erw ‚Üê X.presheaf.germ_res_apply i ‚ü®‚Üëx, hx‚ü© s,
    rw [this, map_zero, map_zero] },
  replace hs : (X.affine_basis_cover.obj j).to_LocallyRingedSpace.to_RingedSpace.basic_open s' = ‚ä•,
  { rw ‚Üê LocallyRingedSpace.preimage_basic_open,
    erw RingedSpace.basic_open_res,
    ext1,
    simp [hs, opens.map] },
  clear_value s',
  revert s',
  erw Veq',
  intros s' hs,
  erw [basic_open_eq_of_affine', prime_spectrum.basic_open_empty_iff] at hs,
  replace hs := hs.map (Spec_Œì_identity.app (X.affine_basis_cover_ring j)).inv,
  erw category_theory.iso.hom_inv_id_apply at hs,
  haveI := is_reduced_of_open_immersion (X.affine_basis_cover.map j),
  apply is_nilpotent.eq_zero,
  exact hs,
end

lemma basic_open_empty_iff {X : Scheme} [is_reduced X] {U : opens X.carrier}
  (s : X.presheaf.obj $ op U) :
  X.to_LocallyRingedSpace.to_RingedSpace.basic_open s = ‚àÖ ‚Üî s = 0 :=
begin
  refine ‚ü®eq_zero_of_basic_open_empty s, _‚ü©,
  rintro rfl,
  simp,
end

@[priority 900]
instance is_reduced_of_is_integral [is_integral X] : is_reduced X :=
begin
  constructor,
  intro U,
  cases U.1.eq_empty_or_nonempty,
  { have : U = ‚àÖ := subtype.eq h,
    haveI := CommRing.subsingleton_of_is_terminal (X.sheaf.is_terminal_of_eq_empty this),
    change _root_.is_reduced (X.sheaf.val.obj (op U)),
    apply_instance },
  { haveI : nonempty U := by simpa, apply_instance }
end

instance is_irreducible_of_is_integral [is_integral X] : irreducible_space X.carrier :=
begin
  by_contradiction H,
  replace H : ¬¨ is_preirreducible (‚ä§ : set X.carrier) := Œª h,
    H { to_preirreducible_space := ‚ü®h‚ü©, to_nonempty := infer_instance },
  simp_rw [is_preirreducible_iff_closed_union_closed, not_forall, not_or_distrib] at H,
  rcases H with ‚ü®S, T, hS, hT, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü©,
  erw not_forall at h‚ÇÇ h‚ÇÉ,
  simp_rw not_forall at h‚ÇÇ h‚ÇÉ,
  haveI : nonempty (‚ü®S·∂ú, hS.1‚ü© : opens X.carrier) := ‚ü®‚ü®_, h‚ÇÇ.some_spec.some_spec‚ü©‚ü©,
  haveI : nonempty (‚ü®T·∂ú, hT.1‚ü© : opens X.carrier) := ‚ü®‚ü®_, h‚ÇÉ.some_spec.some_spec‚ü©‚ü©,
  haveI : nonempty (‚ü®S·∂ú, hS.1‚ü© ‚äî ‚ü®T·∂ú, hT.1‚ü© : opens X.carrier) :=
    ‚ü®‚ü®_, or.inl h‚ÇÇ.some_spec.some_spec‚ü©‚ü©,
  let e : X.presheaf.obj _ ‚âÖ CommRing.of _ := (X.sheaf.is_product_of_disjoint ‚ü®_, hS.1‚ü© ‚ü®_, hT.1‚ü© _)
    .cone_point_unique_up_to_iso (CommRing.prod_fan_is_limit _ _),
  apply_with false_of_nontrivial_of_product_domain { instances := ff },
  { exact e.symm.CommRing_iso_to_ring_equiv.is_domain _ },
  { apply X.to_LocallyRingedSpace.component_nontrivial },
  { apply X.to_LocallyRingedSpace.component_nontrivial },
  { ext x,
    split,
    { rintros ‚ü®hS,hT‚ü©,
      cases h‚ÇÅ (show x ‚àà ‚ä§, by trivial),
      exacts [hS h, hT h] },
    { intro x, exact x.rec _ } }
end

end algebraic_geometry
